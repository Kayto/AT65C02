10 REM MANDELBROT VIEWER
20 PRINT "MANDELBROT SET"
30 PRINT
40 REM SETTINGS
50 W = 30: H = 15
60 XM = -2: XX = 0.5
70 YM = -1: YX = 1
80 MI = 12
90 PRINT "SIZE:"; W; "X"; H
100 PRINT "ITERATIONS:"; MI
110 PRINT
120 REM GENERATE
130 FOR Y = 0 TO H - 1
140 FOR X = 0 TO W - 1
160 CR = XM + X * (XX - XM) / W
170 CI = YM + Y * (YX - YM) / H
180 ZR = 0: ZI = 0: I = 0
190 IF ABS(ZR) > 2 THEN GOTO 270
200 IF ABS(ZI) > 2 THEN GOTO 270
210 ZR2 = ZR * ZR
220 ZI2 = ZI * ZI
230 IF ZR2 + ZI2 > 4 THEN GOTO 270
240 ZT = ZR2 - ZI2 + CR
250 ZI = 2 * ZR * ZI + CI
260 ZR = ZT
265 I = I + 1
266 IF I < MI THEN GOTO 190
270 IF I >= MI THEN PRINT "#";: GOTO 300
280 C = INT(I * 5 / MI)
290 PRINT MID$(" .:+*", C + 1, 1);
300 NEXT X
310 PRINT
320 NEXT Y
330 PRINT
340 PRINT "COMPLETE"
350 END
